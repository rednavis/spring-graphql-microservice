plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.4.RELEASE' apply false
    id 'com.google.cloud.tools.jib' version '2.0.0' apply false
}

ext {
    springCloudVersion = 'Hoxton.SR1'
    springBootVersion = '2.2.4.RELEASE'
    javaxServletApiVersion = '4.0.1'
    swaggerVersion = '2.9.2'
    graphqlKickstartVersion = '6.0.1'
    lombokVersion = '1.18.12'
    mapstructVersion = '1.3.1.Final'
    mockneatVersion = '0.3.8'
    relmongoVersion = '3.2.0'
    jupiterVersion = '5.6.0'
    springtainerVersion = '1.0.0'
    jaxRsVersion = '2.1.1'
}

allprojects {
//    ./gradlew clean htmlDependencyReport
    apply plugin: 'project-report'
//     needed because of multi-module configuration
    apply plugin: 'jacoco'
}

// assumed that all subprojects are java-based modules
subprojects {
    repositories {
        jcenter()
        mavenCentral()
    }

    // needed because of multi-module configuration
    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    apply from: "${rootDir}/gradle/java.gradle"
    apply from: "${rootDir}/gradle/checkstyle.gradle"

    dependencyManagement {
        dependencies {
            //Server
            imports {
                mavenBom "org.springframework.cloud:spring-cloud-starter-parent:${springCloudVersion}"
            }

            dependency "javax.servlet:javax.servlet-api:${javaxServletApiVersion}"

            dependency "io.springfox:springfox-swagger-ui:${swaggerVersion}"
            dependency "io.springfox:springfox-swagger2:${swaggerVersion}"

            dependency "com.graphql-java-kickstart:graphql-spring-boot-starter:${graphqlKickstartVersion}"
            dependency "com.graphql-java-kickstart:graphiql-spring-boot-starter:${graphqlKickstartVersion}"

            dependency "org.projectlombok:lombok:${lombokVersion}"
            dependency "org.mapstruct:mapstruct:${mapstructVersion}"
            dependency "org.mapstruct:mapstruct-processor:${mapstructVersion}"

            dependency "net.andreinc.mockneat:mockneat:${mockneatVersion}"
            dependency "io.github.kaiso.relmongo:relmongo:${relmongoVersion}"

            //Test
            dependency "org.junit.jupiter:junit-jupiter:${jupiterVersion}"
            dependency "com.graphql-java-kickstart:graphql-spring-boot-starter-test:${graphqlKickstartVersion}"
            dependency "com.avides.springboot.springtainer:springtainer-mongodb:${springtainerVersion}"
            dependency "javax.ws.rs:javax.ws.rs-api:${jaxRsVersion}"
        }
    }

    dependencies {
        compile 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        implementation 'net.andreinc.mockneat:mockneat'

        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit'
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

// https://github.com/codecov/example-gradle#-do-you-support-multi-module-projects
codeCoverageReport.dependsOn {
    subprojects*.test
}

wrapper {
    gradleVersion = "6.1.1"
}