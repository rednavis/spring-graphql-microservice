plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.4.RELEASE' apply false
}

ext {
    springCloudVersion = 'Hoxton.SR1'
    javaxServletApiVersion = '4.0.1'
    lombokVersion = '1.18.10'
    jupiterVersion = '5.6.0'
}

allprojects {
    // needed because of multi-module configuration
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_13
    targetCompatibility = JavaVersion.VERSION_13

    compileJava {
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    group 'rednavis'
    version '1.0-SNAPSHOT'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencyManagement {
        dependencies {
            //Server
            imports {
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            }

            dependency("javax.servlet:javax.servlet-api:${javaxServletApiVersion}")
            dependency("org.projectlombok:lombok:${lombokVersion}")

            //Test
            dependency("org.junit.jupiter:junit-jupiter:${jupiterVersion}")
        }
    }

    dependencies {
        compile('org.projectlombok:lombok')
        annotationProcessor('org.projectlombok:lombok')

        testCompile("org.junit.jupiter:junit-jupiter")
        testCompile('org.springframework.boot:spring-boot-starter-test')
    }

    configurations {
        testAnnotationProcessor.extendsFrom annotationProcessor
    }

    test {
        useJUnitPlatform()
    }
}

// assumed that all subprojects are java-based modules
subprojects {

    apply from: "${rootDir}/gradle/java.gradle"
    apply from: "${rootDir}/gradle/checkstyle.gradle"

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

// https://github.com/codecov/example-gradle#-do-you-support-multi-module-projects
codeCoverageReport.dependsOn {
    subprojects*.test
}

wrapper {
    gradleVersion = "6.1.1"
}